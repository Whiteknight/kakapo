#! /usr/bin/env perl
use strict;
use warnings;

use FindBin;
use lib "$FindBin::Bin/../lib";

use File::Spec;
use Getopt::Long qw(:config pass_through);

use TAP::Harness;

my ($slash, $sh_ext);

if ($^O eq 'MSWin32') {
	$slash = '\\';
	$sh_ext = '.cmd';
} else {
	$slash = '/';
	$sh_ext = '.sh';
}

GetOptions(
	'comments'		=> \my $show_comments,
	'failures'		=> \my $show_failures,
	'jobs'			=> \my $jobs,
	'root-dir'		=> \my $root_dir,
	'verbosity=i'		=> \my $verbosity,
);

$root_dir = "$FindBin::Bin/.." unless $root_dir;
$ENV{'HARNESS_ROOT_DIR'} = $root_dir;

my $harness_nqp = $root_dir . '/t/harness-nqp.sh';
$harness_nqp =~ s#/#$slash#g unless $slash eq '/';
die "Cannot find harness-nqp script. Did you set HARNESS_ROOT_DIR?" unless -x $harness_nqp;

my @files;

# Turn args into files, expanding @list-files
for (@ARGV) {
	if (/@(.+)/) {
		open(LIST, '<', $_)
			or die "Could not open test-list file: $!";
			
		while (<LIST>) {
			next if /^\s*$/ || /^\s*#/;
			chomp;

			s/^\s*(.*\S)\s*$/$1/;
			s#/#$slash#g unless $slash eq '/';
			
			if (-r $_) {
				push @files, $_;
			}
			else {
				warn "Missing test file: $_\n";
			}
		}
		
		close(LIST)
			or die "Failed to close test-list file: $!";
	}
	else {
		push @files, $_;
	}
}

my @include = map qr/$_/, (
	qr{ \. nqp $ }x,
);

sub take_file {
	my $path = shift;
	my @blocks = map { $path =~ m/$_/ } @include;
	
	return +@blocks;
}
	
our @exclude = map { qr/\Q$_\E/; } (
	qw(
		/.
		/..
		.svn
		CVS
		.git
	),
);

sub skip_file {
	my $path = shift;

	if (! -r $path) { 
		return 1;
	}
	
	my @blocks = map { $path =~ m/$_/ } @exclude;	
	return +@blocks;
}

sub all_in {
	my $start = shift;
	
	my @files = ();

	if ( $start =~ /^!(.*)/ ) {
		push @exclude, qr{\Q$1\E};
		return @files;
	}
	
	if ( skip_file($start) ) {
		return @files;
	}
	
	if ( -d $start ) {
		if ( opendir(my $dh, $start) ) {
			my @dir_files = sort readdir $dh;
			closedir $dh or die $!;
		
			for my $name ( @dir_files ) {
				my $path = File::Spec->catfile( $start, $name );
				push( @files, all_in( $path ));
			}
		}
	}
	elsif ( take_file( $start )) {
		push( @files, $start );
	}

	return @files;
}

sub exec_test {
	my ($harness, $test_file) = @_;
	
	$test_file =~ /[.]t$/ 
		&& do { return undef; };
	
	$test_file =~ m# \b bootstrap /#x
		&& do { return [ qw( parrot-nqp ), $test_file ]; };
	
	# For now, all tests are run in -nqp. Need to deal later with testing linked files.
	return [ qw( parrot-nqp ), $test_file ];
	#return [ $harness_nqp, $test_file ];
}

my %args = (
	comments	=> $show_comments,
	exec		=> \&exec_test,
	failures	=> $show_failures,
	jobs		=> $jobs,
	verbosity	=> $verbosity,
);

# Turn directories into expanded trees.
my @test_files = map { all_in($_) } sort @files;

my $harness  = TAP::Harness->new( \%args )->runtests(@test_files);

